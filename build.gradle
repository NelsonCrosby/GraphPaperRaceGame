apply plugin: 'groovy'

// Compatible with Java 7
sourceCompatibility = 1.7
// No versions until we actually start working on the project proper
version = '0.0.0'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.slick2d:slick2d-core:1.0.1'
    // Using JUnit for testing (when that happens)
    testCompile 'junit:junit:4.11'
}

// Make natives work
// Adapted from solution provided by Szczepan Faber on
//  http://forums.gradle.org/gradle/topics/how_to_use_lwjgl_or_how_to_use_native_libraries

['windows', 'linux', 'osx'].each { platform ->
    task "${platform}Natives" {
        def outputDir = "lib/natives-$platform"
        inputs.files(configurations.compile)
        outputs.dir(outputDir)
        doLast {
            copy {
                def artifacts = configurations.compile
                        .resolvedConfiguration.resolvedArtifacts
                        .findAll { it.classifier == "natives-$platform" }
                artifacts.each {
                    from zipTree(it.file)
                }
                into outputDir
            }
        }
    }
}
