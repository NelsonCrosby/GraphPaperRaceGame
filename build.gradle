buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
    }
}


apply plugin: 'groovy'
apply plugin: 'eu.appsatori.fatjar'

// Compatible with Java 7
sourceCompatibility = 1.7

version = '0.1.0'

repositories {
    mavenCentral()

    maven {
        // Need this for NCUtils
        url 'https://dl.bintray.com/nelsoncrosby/maven'
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.6'
    compile 'org.slick2d:slick2d-core:1.0.1'
    compile 'io.github.nelsoncrosby:NCUtils:0.1.2'
    // Using JUnit for testing (when that happens)
    testCompile 'junit:junit:4.11'
}

// Make natives work
// Copied from solution provided by Szczepan Faber on
//  http://forums.gradle.org/gradle/topics/how_to_use_lwjgl_or_how_to_use_native_libraries

def platforms = ['windows', 'linux', 'osx']
platforms.each { platform ->
    task "${platform}Natives" {
        def outputDir = "lib/natives-$platform"
        inputs.files(configurations.compile)
        outputs.dir(outputDir)
        doLast {
            copy {
                def artifacts = configurations.compile
                        .resolvedConfiguration.resolvedArtifacts
                        .findAll { it.classifier == "natives-$platform" }
                artifacts.each {
                    from zipTree(it.file)
                }
                into outputDir
            }
        }
    }
}

fatJar {
    classifier = 'dist'

    manifest {
        attributes('Main-Class': 'io.github.nelsoncrosby.gprg.GPRGame')
    }
}
